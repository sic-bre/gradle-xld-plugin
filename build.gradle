plugins {
  id "com.gradle.plugin-publish" version "com.gradle.publish:plugin-publish-plugin:0.9.7"
  id "com.github.hierynomus.license" version "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
  id "nebula.nebula-release" version "com.netflix.nebula:nebula-release-plugin:4.0.1"
  id "com.jfrog.bintray" version "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
}

apply plugin: 'groovy'

dependencies {
  compile gradleApi()
  compile localGroovy()
  compile "com.xebialabs.deployit.engine:remote-booter:2015.6.10"
  compile "nl.javadude.scannit:scannit:1.4.1"
  compile "com.jcraft:jzlib:1.1.3"
  compile "com.google.guava:guava:22.0-android"

  testCompile group: 'junit', name: 'junit', version: '4.12'

}

release {
  defaultVersionStrategy = nebula.plugin.release.NetflixOssStrategies.SNAPSHOT
}
tasks.release.dependsOn tasks.publishPlugins
tasks.release.dependsOn tasks.bintrayUpload
tasks.releaseCheck.dependsOn tasks.clean
tasks.releaseCheck.dependsOn tasks.licenseTest
if (!project.hasProperty('release.scope')) {
  project.ext['release.scope'] = 'patch'
}

clean.doLast {
  project.delete 'src/test/resources/HelloDeployment/build'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

pluginBundle {
  website = 'https://github.com/xebialabs-community/gradle-xld-plugin'
  vcsUrl = 'https://github.com/xebialabs-community/gradle-xld-plugin.git'
  tags = ['deployment', 'deploy', 'xldeploy', 'cd', 'continuous delivery']
  description = """\
This Gradle plugin allows you to deploy your application to an environment as a part of the build process. It uses XL Deploy (http://xebialabs.com/products/xl-deploy/) server to perform the deployment.

Please find more information and usage instructions on the GitHub project page (https://github.com/xebialabs-community/gradle-xld-plugin/blob/master/README.md).

NOTE: THIS PLUGIN DEPENDS ON SOME ADDITIONAL REPOSITORIES:
buildscript {
  repositories {
    maven {
      url "http://www.knopflerfish.org/maven2/"
    }
    maven {
      url "https://dist.xebialabs.com/public/maven2/"
    }
  }
}
plugins {
  id "com.xebialabs.xl-deploy" version "x.x.x"
}
"""

  plugins {
    xlDeployPlugin {
      id = 'com.xebialabs.xl-deploy'
      displayName = 'Gradle XL Deploy plugin'
    }
  }

  mavenCoordinates {
    groupId = 'com.xebialabs.gradle'
    artifactId = 'xl-deploy-gradle-plugin'
  }
}

license {
  header rootProject.file('src/main/license/xebialabs_community.license')
  strictCheck true
  exclude "**/*.txt"
  excludes(["src/test/resources/HelloDeployment/.gradle/**", "src/test/resources/HelloDeployment/userHome/**"])
}
// workaround for a bug in license plugin
tasks.licenseTest.doFirst {
  delete 'src/test/resources/HelloDeployment/userHome/caches/modules-2/'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url "http://www.knopflerfish.org/maven2/"
  }
  maven {
    url "https://dist.xebialabs.com/public/maven2/"
  }
}

apply from: 'extra_publication.gradle'
